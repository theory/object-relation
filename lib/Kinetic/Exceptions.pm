package Kinetic::Exceptions;

# $Id$

use strict;
use Kinetic::Context;

use Exception::Class(
    'Kinetic::Exception' => {
        description => 'Kinetic Exception',
    },

    ##########################################################################
    # Unlocalized exceptions.
    ##########################################################################
    # XXX Perhaps there's a way to get Class::Meta and others to localize
    # exceptions?
    'Kinetic::Exception::ExternalLib' => {
        description => 'External library exception',
        alias       => 'throw_exlib',
    },

    ##########################################################################
    # Fatal exceptions.
    ##########################################################################

    'Kinetic::Exception::Fatal' => {
        description => 'Kinetic fatal exception',
        isa         => 'Kinetic::Exception',
        alias       => 'throw_fatal',
    },

    'Kinetic::Exception::Fatal::Invalid' => {
        description => 'Invalid data',
        isa         => 'Kinetic::Exception::Fatal',
        alias       => 'throw_invalid',
    },

    'Kinetic::Exception::Fatal::ReadOnly' => {
        description => 'Assignment to read-only value',
        isa         => 'Kinetic::Exception::Fatal',
        alias       => 'throw_read_only',
    },

    'Kinetic::Exception::Fatal::Language' => {
        description => 'Localization exception',
        isa         => 'Kinetic::Exception::Fatal',
        alias       => 'throw_lang',
    },

    'Kinetic::Exception::Fatal::Stat' => {
        description => 'File status exception',
        isa         => 'Kinetic::Exception::Fatal',
        alias       => 'throw_stat',
    },

    'Kinetic::Exception::Fatal::IO' => {
        description => 'File IO exception',
        isa         => 'Kinetic::Exception::Fatal',
        alias       => 'throw_io',
    },

    ##########################################################################
    # Non-fatal errors.
    ##########################################################################

    'Kinetic::Exception::Error' => {
        description => 'Kinetic error',
        isa         => 'Kinetic::Exception',
        alias       => 'throw_error',
    },

    'Kinetic::Exception::Error::Password' => {
        description => 'Kinetic password error',
        isa         => 'Kinetic::Exception::Error',
        alias       => 'throw_password',
    },


);

Kinetic::Exception->Trace(1);

use Exporter::Tidy all => [
  qw(isa_kinetic_exception isa_exception throw_exlib throw_fatal throw_invalid
       throw_read_only throw_lang throw_stat throw_io throw_error
       throw_password)
];

##############################################################################

=head1 Name

Kinetic::Exceptions - Kinetic exception object definitions

=head1 Synopsis

  use Kinetic::Exceptions ':all';
  throw_fatal 'Whoops!';

  # The error is fully localizable.
  throw_fatal ['Unknown value "[_1]", 'foo'];

=head1 Description

This class defines Kinetic exception objects. It subclasses Exception::Class,
which provides a robust exception implementation. It extends Exception::Class
by requiring localizable error messages. All error messages must be
represented in the appropriate Kinetic::Language lexicons.

There currently three major classes of exceptions:

=over

=item Kinetic::Exception::Fatal

This class and its subclasses represent fatal, non-recoverable errors.
Exceptions of this sort are unexpected, and should be reported to an
administrator or to the Kinetic developers.

=item Kinetic::Exception::Error

This clas and its subclasses represent non-fatal errors triggered by invalid
data. These can be used to let users know that the data they've entered is
invalid.

=item Kinetic::Exception::ExternalLib

This class inherits from Exception::Class rather than from Kinetic::Exception
so that it can be used for exceptions thrown by libraries not under direct
Kinetic control and therefore are not localizable.

=back

=cut

##############################################################################

=head1 Exception Classes

The exception classes generated by this module are as follows:

=over 4

=item Kinetic::Exception

Kinetic exception base class. It generally should not be thrown, only its
subclasses.

=item Kinetic::Exception::Fatal

Base class for fatal exceptions. Alias: C<throw_fatal>.

=item Kinetic::Exception::Fatal::Invalid

Invalid data exception. Thrown when an invalid value is assigned to a Kinetic
class attribute. Alias: C<throw_invalid>.

=item Kinetic::Exception::Error

Base class for error exceptions. These are non-fatal errors, generally
triggered by problems with data entered by users. Alias: C<throw_error>.

=back

=cut

##############################################################################

=head1 Interface

=head2 Functions

In addition to the functions that can be imported to throw the above
exceptions, there are a few other functions that may be imported into a client
class.

=head3 isa_kinetic_exception

  if (isa_kinetic_exception($@))
      print 'A Kinetic exception was thrown';
      print "...and it was fatal!' if isa_kinetic_exception($@, 'Fatal');
  }

This function returns true if the argument passed to it is a Kinetic
exception. The optional second argument can be used to test for a specific
Kinetic exception. If no such exception exists, an exception will be thrown.

=cut

sub isa_kinetic_exception {
    my ($err, $name) = @_;
    return unless $err;

    my $class = "Kinetic::Exception";
    if ($name) {
        $class .= "::$name";
        throw_fatal qq{No such exception class "$class"}
          unless $class->VERSION;
    }

    return UNIVERSAL::isa($err, $class);
}

##############################################################################

=head3 isa_exception

  if (isa_exception($@)) {
      print "What we have here...is an exception object";
  }

This function returns true if the argument passed to it is an Exception::Class
object.

=cut

sub isa_exception {
    my $err = shift;
    return $err && UNIVERSAL::isa($err, 'Exception::Class::Base');
}

##############################################################################
# From here on in, we're modifying the behavior of Exception::Class::Base.

package Kinetic::Exception;

sub new {
    my $class = shift;
    my %p =  @_ == 1 ? ( error => $_[0] ) : @_;
    # Localize the error message.
    $p{error} = Kinetic::Context->language->maketext(
        ref $p{error} ? @{$p{error}} : $p{error}
    );
    $class->SUPER::new(%p);
}

1;
__END__

##############################################################################

=head1 Author

Kineticode, Inc. <info@kineticode.com>

=head1 See Also

=over 4

=item L<Kinetic::Base|Kinetic::Base>

The Kinetic base class.

=item L<Exception::Class|Exception::Class>

Module used to define Kinetic exceptions.

=back

=head1 Copyright and License

Copyright (c) 2004 Kineticode, Inc.

This Library is free software; you can redistribute it and/or modify it under
the same terms as Perl itself.

=cut
